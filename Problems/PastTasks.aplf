 PastTasks←{                                                 ⍝  Retrieve all links to PDFs from page at url.
     url←⍵                                                   ⍝  Requires ]load HttpCommand.
     getXML←{⎕XML(#.HttpCommand.Get ⍵).Data}                 ⍝  XML matrix of page at URL ⍵.
     getRows←{                                               ⍝  Rows of mat containing key in column col.
         col key←⍺
         mat←⍵
         (key∘≡¨mat[;col])⌿mat
     }                                                       ⍝  Columns for element names, attribute matrices,
     name attr key val←1 3 0 1                               ⍝  and keys & values in attribute matrices.
     getCol←⌷[1]                                             ⍝  Column ⊣ in matrix ⊢.
     getAttrs←attr∘getCol                                    ⍝  Attribute matrices from all rows of XML submatrix ⍵.
     getVals←val∘getCol                                      ⍝  Values from all rows of attribute submatrix ⍵.
     getBase←name'base'∘getRows                              ⍝  Base element rows from XML matrix ⊢.
     getAnchors←name(,'a')∘getRows                           ⍝  Anchor element rows from XML matrix ⊢.
     getHrefs←((⊃,/)getVals∘(key'href'∘getRows)¨)getAttrs    ⍝  href URLs from XML submatrix ⊢.
     getPDFs←⊢(/⍨)('.pdf'≡¯4↑⊢)¨                             ⍝  Links to PDFs from URL list ⊢.
     xml←getXML url
     baseAddress←⊃getHrefs getBase xml
     baseAddress∘,¨getPDFs getHrefs getAnchors xml
 }
