 revp←{

     ⎕IO←0

     ⍺←4+⍳9
     lens←⍺
     dna←⍵

     ⍝ Return indices and lengths of reverse palindrome substrings of dna,
     ⍝ each of length ∊ lens.

     ⍝ Reverse complement of ⍵.
     flip←{⌽'TACG'['ATGC'⍳⍵]}

     ⍝ If ⊢ is a reverse palindrome.
     isRevp←⊢≡flip

     ⍝ All starts and lengths of substrings with length ∊ ⊢
     ⍝ of a string of length ⊣.
     subIds←(⊃,/)⊢,¨⍨∘⍳¨1+-
     ⍝ subs←{⊃,/⍵,¨⍨∘⍳¨1+⍺-⍵}

     ⍝ The substring of dna of start and length ⊢.
     sub←⊃↑∘(↓∘dna)⍨/

     ⍝ ⎕IO←0 correction
     1 0+[1]↑(⊢(/⍨)isRevp∘sub¨)lens subIds⍨≢dna

 }
